plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

def version_str = '2.2.8'

group = 'io.github.paxtechnologyinc'
version = version_str

def static releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}
def sdkDestinationPath = "build/jar/libs/"
def SDK_BASENAME = "test_jar_sdk"
def SDK_VERSION = "_V1.00.00_" + releaseTime()

java {
    withJavadocJar()
    withSourcesJar()
}

task makeJar(type: Jar) {
    baseName SDK_BASENAME + SDK_VERSION
    destinationDir file(sdkDestinationPath)
}


publishing {
    publications {
//        relocation(MavenPublication) {
//            pom {
//                // Old artifact coordinates
//                groupId = "io.github.paxtechnologyinc"
//                artifactId = "test_jar"
//                version = '2.2.7'
//
//                distributionManagement {
//                    relocation {
//                        // New artifact coordinates
//                        groupId = "io.github.paxtechnologyinc"
//                        artifactId = "test_jar"
//                        version = version_str
//                        message = "version has been changed"
//                    }
//                }
//            }
//        }
        mavenJava(MavenPublication) {
            artifactId = 'test_jar'
            from components.java
            // Additional metadata
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Test Jar Project'
                description = 'A test project for demonstration'
                url = 'https://github.com/PAXTechnologyInc/test_jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/PAXTechnologyInc/test_jar.git'
                    developerConnection = 'scm:git:ssh://git@github.com/PAXTechnologyInc/test_jar.git'
                    url = 'https://github.com/PAXTechnologyInc/test_jar'
                }
                developers {
                    developer {
                        id = 'Ruize_Ren'
                        name = 'Ruize Ren'
                        email = 'ruize.ren@pax.us'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'Sonatype'
            def releasesRepoUrl= 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl= 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = project.findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}